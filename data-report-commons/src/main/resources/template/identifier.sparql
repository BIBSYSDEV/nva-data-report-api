# If the selected variables are changed, update the documentation in identifierReport.md

PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT DISTINCT
    ?publicationId
    ?status
    ?publicationIdentifier
    ?additionalIdentifierSource
    ?additionalIdentifier
    ?additionalIdentifierType
    ?modifiedDate
WHERE {
    ?uri a :Publication ;
                    :status ?publicationStatus ;
                    :modifiedDate ?modifiedDateRaw ;
                    :identifier ?publicationIdentifier ;
                    :additionalIdentifier ?additionalIdentifierNode .

    BIND(STR(?uri) AS ?publicationId)
    BIND(REPLACE(STR(?publicationStatus), "https://nva.sikt.no/ontology/publication#", "", "i") AS ?status)
    BIND(STR(?modifiedDateRaw) AS ?modifiedDate)

    ?additionalIdentifierNode a ?additionalIdentifierTypeRaw .
    ?additionalIdentifierNode :sourceName ?additionalIdentifierSource .
    ?additionalIdentifierNode :value ?additionalIdentifier .
    BIND(REPLACE(STR(?additionalIdentifierTypeRaw), "https://nva.sikt.no/ontology/publication#", "", "i") AS ?additionalIdentifierType)
}