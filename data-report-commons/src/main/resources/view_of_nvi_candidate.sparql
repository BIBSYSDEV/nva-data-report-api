PREFIX : <https://nva.sikt.no/ontology/publication#>

##Flattening reportingPeriod.year -> reportingYear
##Flattening publicationDetails.contributor -> nviContributor

CONSTRUCT {
  ?uri a :NviCandidate .
  ?uri :identifier ?identifier .
  ?uri :isApplicable ?isApplicable .
  ?uri :internationalCollaborationFactor ?internationalCollaborationFactor .
  ?uri :globalApprovalStatus ?globalApprovalStatus .
  ?uri :publicationTypeChannelLevelPoints ?publicationTypeChannelLevelPoints .
  ?uri :creatorShareCount ?creatorShareCount .
  ?uri :modifiedDate ?modifiedDate .
  ?uri :reportingYear ?reportingYear .
  ?uri :reported ?reported .
  ?uri :publicationId ?publicationId .
  ?uri :nviContributor ?nviContributor .
  ?uri :approval ?approval .
  ?uri :totalPoints ?totalPoints .

  ?nviContributor :nviAffiliation ?nviAffiliation .

  ?approval :institutionId ?institutionId .
  ?approval :approvalStatus ?approvalStatus .
  ?approval :institutionPoints ?institutionPoints .
  ?approval :creatorAffiliationPoints ?creatorAffiliationPoints .
  ?approval :involvedOrganization ?involvedOrganization .

  ?creatorAffiliationPoints :nviCreator ?nviCreator .
  ?creatorAffiliationPoints :points ?points .
  ?creatorAffiliationPoints :affiliation ?affiliation .

} WHERE {
  ?uri :identifier ?identifier .
  ?uri :isApplicable ?isApplicable .
  ?uri :internationalCollaborationFactor ?internationalCollaborationFactor .
  ?uri :globalApprovalStatus ?globalApprovalStatus .
  ?uri :publicationTypeChannelLevelPoints ?publicationTypeChannelLevelPoints .
  ?uri :creatorShareCount ?creatorShareCount .
  ?uri :modifiedDate ?modifiedDate .

  ?uri :reportingPeriod ?reportingPeriod .
  ?uri :reported ?reported .
  ?uri :publicationDetails ?publicationId .
  ?uri :approval ?approval .
  ?uri :points ?totalPoints .

  ?reportingPeriod :year ?reportingYear .

  ?publicationId :contributor ?nviContributor .

  ?nviContributor a :NviContributor .
  ?nviContributor :affiliation ?nviAffiliation .
  ?nviAffiliation a :NviOrganization .

  ?approval a :Approval .
  ?approval :institutionId ?institutionId .
  ?approval :approvalStatus ?approvalStatus .
  ?approval :points ?pointsDistribution .
  ?approval :involvedOrganization ?involvedOrganization .

  ?pointsDistribution :creatorAffiliationPoints ?creatorAffiliationPoints .
  ?pointsDistribution :institutionPoints ?institutionPoints .

  ?creatorAffiliationPoints a :CreatorAffiliationPoints .
  ?creatorAffiliationPoints :nviCreator ?nviCreator .
  ?creatorAffiliationPoints :points ?points .
  ?creatorAffiliationPoints :affiliationId ?affiliation .


}