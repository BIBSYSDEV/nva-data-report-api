plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco-report-aggregation'
}

group = 'no.sikt'

dependencies {
    testImplementation libs.bundles.testing
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    // source-code version and must be <= targetCompatibility
    targetCompatibility = JavaVersion.VERSION_21 // bytecode target version
}

tasks.named('test') {
    useJUnitPlatform()
    failFast = false
    testLogging {
        events 'skipped', 'passed', 'failed'
    }
}

pmd {
    toolVersion = '7.11.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}
tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required
        html.required
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {

    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }
    }
}
