PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
  ?publicationId
  ?status
  ?publicationIdentifier
  ?contributorIdentifier
  ?contributorName
  ?contributorSequenceNumber
  ?contributorRole
  ?modifiedDate
  WHERE {
    GRAPH ?g {
      ?uri :status ?publicationStatus ;
           :modifiedDate ?modifiedDateRaw ;
           :identifier ?publicationIdentifier ;
           :contributor ?contributor .

      FILTER(?modifiedDateRaw >= "__AFTER__"^^xsd:dateTime)
      FILTER(?modifiedDateRaw < "__BEFORE__"^^xsd:dateTime)

      BIND(REPLACE(STR(?publicationStatus), "^.*/([^/]*)$", "$1") AS ?status)
      BIND(STR(?modifiedDateRaw) AS ?modifiedDate)
      BIND(STR(?uri) AS ?publicationId)

      ?contributor :name ?contributorName ;
                   :role ?roleType ;
                   :sequence ?contributorSequenceNumber .

      BIND(REPLACE(STR(?contributor), "^.*/([^/]*)$", "$1") AS ?contributorIdentifier)
      BIND(REPLACE(STR(?roleType), "https://nva.sikt.no/ontology/publication#", "", "i") AS ?contributorRole)
    }
} LIMIT __PAGE_SIZE__ OFFSET __OFFSET__