PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
    ?publicationId
    ?status
    ?publicationIdentifier
    ?fundingSource
    ?fundingId
    ?fundingName
    ?modifiedDate
WHERE {
  GRAPH ?g {
    ?uri :status ?publicationStatus ;
         :modifiedDate ?modifiedDateRaw ;
         :identifier ?publicationIdentifier ;
         :funding ?funding .

    FILTER(?modifiedDateRaw >= "__AFTER__"^^xsd:dateTime)
    FILTER(?modifiedDateRaw < "__BEFORE__"^^xsd:dateTime)

    BIND(STR(?uri) AS ?publicationId)
    BIND(REPLACE(STR(?publicationStatus), "^.*/([^/]*)$", "$1") AS ?status)
    BIND(STR(?modifiedDateRaw) AS ?modifiedDate)

    ?funding :fundingSource ?fundingSource .
    ?funding :identifier ?fundingId .
    ?funding :label ?label .
    FILTER(LANG(?label) = "en")
    BIND(STR(?label) AS ?fundingName)
  }
} LIMIT __PAGE_SIZE__ OFFSET __OFFSET__