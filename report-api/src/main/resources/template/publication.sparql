PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
    ?publicationId
    ?status
    ?publicationTitle
    ?publicationCategory
    ?publicationDate
    ?channelType
    ?channelIdentifier
    ?channelName
    ?channelOnlineIssn
    ?channelPrintIssn
    ?channelLevel
    ?publicationIdentifier
    ?modifiedDate
WHERE {
  GRAPH ?g {
    ?uri a ?instanceType ;
         :status ?publicationStatus ;
         :modifiedDate ?modifiedDateRaw ;
         :identifier ?publicationIdentifier ;
         :year ?year ;
         :mainTitle ?publicationTitle ;
         :publicationContext ?publicationContext .

    FILTER(?modifiedDateRaw >= "__AFTER__"^^xsd:dateTime)
    FILTER(?modifiedDateRaw < "__BEFORE__"^^xsd:dateTime)

    BIND(STR(?uri) AS ?publicationId)
    BIND(REPLACE(STR(?instanceType), "https://nva.sikt.no/ontology/publication#", "", "i") AS ?publicationCategory)
    BIND(REPLACE(STR(?publicationStatus), "^.*/([^/]*)$", "$1") AS ?status)
    BIND(STR(?modifiedDateRaw) AS ?modifiedDate)

    OPTIONAL {
        ?uri :month ?month .
    }
    OPTIONAL {
        ?uri :day ?day .
    }

    BIND(IF(BOUND(?month), CONCAT("-", ?month), "") AS ?monthPart)
    BIND(IF(BOUND(?day), CONCAT("-", ?day), "") AS ?dayPart)
    BIND(CONCAT(?year, CONCAT(?monthPart, ?dayPart)) AS ?publicationDate)

    ?publicationContext a ?channelTypeId ;
                        :name ?channelName ;
                        :identifier ?channelIdentifier ;
                        :scientificValue ?channelLevel .

    BIND(STR(?channelTypeId) AS ?channelType)

    OPTIONAL {
        ?publicationContext :onlineIssn ?channelOnlineIssn
    }
    OPTIONAL {
        ?publicationContext :printIssn ?channelPrintIssn
    }
  }
} LIMIT __PAGE_SIZE__ OFFSET __OFFSET__