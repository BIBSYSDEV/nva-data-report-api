PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
  ?publicationId
  ?contributorIdentifier
  ?affiliationId
  ?institutionId
  ?institutionPoints
  ?pointsForAffiliation
  ?institutionApprovalStatus
  ?globalApprovalStatus
  ?reportedPeriod
  ?totalPoints
  ?publicationTypeChannelLevelPoints
  ?authorShareCount
  ?internationalCollaborationFactor
  ?isApplicable
WHERE {
  GRAPH ?g {
    {
      ?candidate a :NviCandidate ;
        :modifiedDate ?modifiedDate ;
        :publicationDetails ?publicationUri ;
        :isApplicable ?isApplicableBoolean ;
        :points ?totalPoints ;
        :globalApprovalStatus ?globalApprovalStatus ;
        :internationalCollaborationFactor ?internationalCollaborationFactor ;
        :creatorShareCount ?authorShareCount ;
        :publicationTypeChannelLevelPoints ?publicationTypeChannelLevelPoints .

      OPTIONAL {
        ?candidate :reported ?reported ;
                   :reportingPeriod ?reportingPeriod .
        ?reportingPeriod a :ReportingPeriod ;
                         :year ?reportedYear .
        BIND(IF(?reported = true, ?reportedYear, "NotReported") AS ?reportedPeriod)
      }

      BIND(STR(?publicationUri) AS ?publicationId)
      BIND(STR(?isApplicableBoolean) AS ?isApplicable)

      ?publicationUri :contributor ?contributorId .
      ?contributorId a :NviContributor .
      BIND(REPLACE(STR(?contributorId), "^.*/([^/]*)$", "$1") AS ?contributorIdentifier)

      ?contributorId :affiliation ?affiliationUri .
      ?affiliationUri a ?NviOrganization .
      BIND(STR(?affiliationUri) AS ?affiliationId)

      ?candidate :approval ?approval .
      ?approval a :Approval ;
        :approvalStatus ?institutionApprovalStatus ;
        :points ?institutionPoints ;
        :institutionId ?organizationUri .
      FILTER (?affiliationUri = ?organizationUri || EXISTS { ?affiliationUri :partOf ?organizationUri . })
      BIND(STR(?organizationUri) AS ?institutionId)

      FILTER(?modifiedDate >= "__AFTER__"^^xsd:dateTime)
      FILTER(?modifiedDate < "__BEFORE__"^^xsd:dateTime)

      {
        SELECT ?publicationUri ?organizationUri (COUNT(DISTINCT ?contributorId) AS ?numberOfAffiliationsForInstitution){
          ?candidate a :NviCandidate ;
                  :modifiedDate ?modifiedDate ;
                  :publicationDetails ?publicationUri ;
                  :approval ?approval .

          ?publicationUri :contributor ?contributorId .
                ?contributorId a :NviContributor .

          ?contributorId :affiliation ?affiliationUri .
          ?affiliationUri a ?NviOrganization .

          ?approval a :Approval ;
            :institutionId ?organizationUri .

          FILTER (?affiliationUri = ?organizationUri || EXISTS { ?affiliationUri :partOf ?organizationUri . })
        }
        GROUP BY ?publicationUri ?organizationUri
      }
      BIND(STR(ROUND((xsd:decimal(?institutionPoints)*10000)/?numberOfAffiliationsForInstitution)/10000) AS ?pointsForAffiliation)
    }
    UNION
    {
      ?candidate a :NviCandidate ;
        :modifiedDate ?modifiedDate ;
        :publicationDetails ?publicationUri ;
        :isApplicable ?isApplicableBoolean .
      BIND(STR(?publicationUri) AS ?publicationId)
      BIND(STR(?isApplicableBoolean) AS ?isApplicable)

      FILTER(?isApplicableBoolean = false)
      FILTER(?modifiedDate >= "__AFTER__"^^xsd:dateTime)
      FILTER(?modifiedDate < "__BEFORE__"^^xsd:dateTime)
    }
  }
} LIMIT __PAGE_SIZE__ OFFSET __OFFSET__