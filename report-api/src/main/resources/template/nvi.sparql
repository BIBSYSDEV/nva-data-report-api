PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
  ?publicationId
  ?contributorIdentifier
  ?affiliationId
  ?institutionId
  ?institutionPoints
  ?pointsForAffiliation
  ?institutionApprovalStatus
  ?globalApprovalStatus
  ?reportedPeriod
  ?totalPoints
  ?publicationTypeChannelLevelPoints
  ?authorShareCount
  ?internationalCollaborationFactor
  ?isApplicable
WHERE {
  GRAPH ?g {
    {
      ?candidate a :NviCandidate ;
        :modifiedDate ?modifiedDate ;
        :publicationId ?publicationUri ;
        :isApplicable ?isApplicableBoolean ;
        :totalPoints ?totalPoints ;
        :globalApprovalStatus ?globalApprovalStatus ;
        :internationalCollaborationFactor ?internationalCollaborationFactorDecimal ;
        :creatorShareCount ?authorShareCount ;
        :publicationTypeChannelLevelPoints ?publicationTypeChannelLevelPointsDecimal ;
        :reportingYear ?reportingYear ;
        :reported ?reported ;
        :nviContributor ?nviContributor ;
        :approval ?approval .

      FILTER(?modifiedDate >= "__AFTER__"^^xsd:dateTime)
      FILTER(?modifiedDate < "__BEFORE__"^^xsd:dateTime)

      BIND(STR(?publicationUri) AS ?publicationId)

      BIND(STR(?internationalCollaborationFactorDecimal) AS ?internationalCollaborationFactor)
      BIND(STR(?publicationTypeChannelLevelPointsDecimal) AS ?publicationTypeChannelLevelPoints)

      BIND(IF(?reported = true, ?reportedYear, "NotReported") AS ?reportedPeriod)

      BIND(STR(?isApplicableBoolean) AS ?isApplicable)

      BIND(STR(?nviContributor) AS ?contributorIdString)
      BIND(REPLACE(STR(?contributorId), "^.*/([^/]*)$", "$1") AS ?contributorIdentifier)

      ?nviContributor :nviAffiliation ?affiliationUri .
      BIND(STR(?affiliationUri) AS ?affiliationId)

      ?approval :approvalStatus ?institutionApprovalStatus ;
                :institutionPoints ?institutionPoints ;
                :creatorAffiliationPoints ?creatorAffiliationPoints ;
                :institutionId ?organizationUri ;
                :involvedOrganization ?affiliationId .

      BIND(STR(?organizationUri) AS ?institutionId)

      ?creatorAffiliationPoints :nviCreator ?contributorIdString ;
                                :affiliationId ?affiliationId ;
                                :points ?pointsForAffiliationDouble .

      BIND(STR(?pointsForAffiliationDouble) AS ?pointsForAffiliation)
    }
    UNION
    {
      ?candidate a :NviCandidate ;
        :modifiedDate ?modifiedDate ;
        :publicationId ?publicationUri ;
        :isApplicable ?isApplicableBoolean .

      FILTER(?modifiedDate >= "__AFTER__"^^xsd:dateTime)
      FILTER(?modifiedDate < "__BEFORE__"^^xsd:dateTime)

      BIND(STR(?publicationUri) AS ?publicationId)
      BIND(STR(?isApplicableBoolean) AS ?isApplicable)

      FILTER(?isApplicableBoolean = false)

    }
  }
} LIMIT __PAGE_SIZE__ OFFSET __OFFSET__