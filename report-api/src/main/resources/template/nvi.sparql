PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
  ?publicationId
  ?contributorIdentifier
  ?affiliationId
  ?institutionId
  ?institutionPoints
  ?pointsForAffiliation
  ?institutionApprovalStatus
  ?globalApprovalStatus
  ?reportedPeriod
  ?totalPoints
  ?publicationTypeChannelLevelPoints
  ?authorShareCount
  ?internationalCollaborationFactor
  ?isApplicable
WHERE {
  GRAPH ?g {
    {
      ?candidate a :NviCandidate ;
        :modifiedDate ?modifiedDate ;
        :publicationDetails ?publicationUri ;
        :isApplicable ?isApplicableBoolean ;
        :points ?totalPoints ;
        :globalApprovalStatus ?globalApprovalStatus ;
        :internationalCollaborationFactor ?internationalCollaborationFactorDecimal ;
        :creatorShareCount ?authorShareCount ;
        :publicationTypeChannelLevelPoints ?publicationTypeChannelLevelPointsDecimal .

      FILTER(?modifiedDate >= "__AFTER__"^^xsd:dateTime)
      FILTER(?modifiedDate < "__BEFORE__"^^xsd:dateTime)

      BIND(STR(?internationalCollaborationFactorDecimal) AS ?internationalCollaborationFactor) .
      BIND(STR(?publicationTypeChannelLevelPointsDecimal) AS ?publicationTypeChannelLevelPoints) .

      OPTIONAL {
        ?candidate :reported ?reported ;
                   :reportingPeriod ?reportingPeriod .
        ?reportingPeriod a :ReportingPeriod ;
                         :year ?reportedYear .
        BIND(IF(?reported = true, ?reportedYear, "NotReported") AS ?reportedPeriod)
      }

      BIND(STR(?publicationUri) AS ?publicationId)
      BIND(STR(?isApplicableBoolean) AS ?isApplicable)

      ?publicationUri :contributor ?contributorId .
      ?contributorId a :NviContributor .
      BIND(STR(?contributorId) AS ?contributorIdString)
      BIND(REPLACE(STR(?contributorId), "^.*/([^/]*)$", "$1") AS ?contributorIdentifier)

      ?contributorId :affiliation ?affiliationUri .
      ?affiliationUri a ?NviOrganization .
      BIND(STR(?affiliationUri) AS ?affiliationId)

      ?candidate :approval ?approval .
      ?approval a :Approval ;
        :approvalStatus ?institutionApprovalStatus ;
        :points ?institutionPointsNode ;
        :institutionId ?organizationUri .
      FILTER (?affiliationUri = ?organizationUri || EXISTS { ?affiliationUri :partOf ?organizationUri . })
      BIND(STR(?organizationUri) AS ?institutionId)

      ?institutionPointsNode a :InstitutionPoints ;
        :points ?institutionPoints ;
        :creatorAffiliationPoints ?creatorAffiliationPoints .

      ?creatorAffiliationPoints a :CreatorAffiliationPoints ;
        :nviCreator ?contributorIdString ;
        :affiliationId ?affiliationId ;
        :points ?pointsForAffiliationDouble .

      BIND(STR(?pointsForAffiliationDouble) AS ?pointsForAffiliation)
    }
    UNION
    {
      ?candidate a :NviCandidate ;
        :modifiedDate ?modifiedDate ;
        :publicationDetails ?publicationUri ;
        :isApplicable ?isApplicableBoolean .

      FILTER(?modifiedDate >= "__AFTER__"^^xsd:dateTime)
      FILTER(?modifiedDate < "__BEFORE__"^^xsd:dateTime)

      BIND(STR(?publicationUri) AS ?publicationId)
      BIND(STR(?isApplicableBoolean) AS ?isApplicable)

      FILTER(?isApplicableBoolean = false)

    }
  }
} LIMIT __PAGE_SIZE__ OFFSET __OFFSET__