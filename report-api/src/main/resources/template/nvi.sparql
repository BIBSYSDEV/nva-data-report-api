PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
  ?publicationId
  ?contributorIdentifier
  ?affiliationId
  ?institutionId
  ?institutionPoints
  ?institutionApprovalStatus
  WHERE {
    GRAPH ?g {
      ?candidate a :NviCandidate ;
        :modifiedDate ?modifiedDate ;
        :publicationDetails ?publicationUri .
      BIND(STR(?publicationUri) AS ?publicationId)

      ?publicationUri :contributor ?contributorId .
      ?contributorId a :NviContributor .
      BIND(REPLACE(STR(?contributorId), "^.*/([^/]*)$", "$1") AS ?contributorIdentifier)

      ?contributorId :affiliation ?affiliationUri .
      ?affiliationUri a ?NviOrganization .
      BIND(STR(?affiliationUri) AS ?affiliationId)

      ?approval a :Approval ;
        :approvalStatus ?institutionApprovalStatus ;
        :points ?institutionPoints ;
        :institutionId ?organizationUriString .
      BIND(URI(?organizationUriString) AS ?organizationUri)
      ?affiliationUri :partOf ?organizationUri .
      BIND(STR(?organizationUri) AS ?institutionId)

      FILTER(?modifiedDate >= "__AFTER__"^^xsd:dateTime)
      FILTER(?modifiedDate < "__BEFORE__"^^xsd:dateTime)
  }
} ORDER BY ?publicationId LIMIT __PAGE_SIZE__