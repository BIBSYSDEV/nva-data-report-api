PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
    ?publicationId
    ?status
    ?publicationIdentifier
    ?contributorId
    ?contributorName
    ?affiliationId
    ?affiliationName
    ?institutionId
    ?facultyId
    ?departmentId
    ?groupId
WHERE {
  GRAPH ?g {
    ?uri :identifier ?publicationIdentifier ;
         :status ?publicationStatus ;
         :modifiedDate ?modifiedDate ;
         :contributor ?contributor .

    FILTER(?modifiedDate >= "__AFTER__"^^xsd:dateTime)
    FILTER(?modifiedDate < "__BEFORE__"^^xsd:dateTime)

    BIND(REPLACE(STR(?publicationStatus), "^.*/([^/]*)$", "$1") AS ?status)
    BIND(STR(?uri) AS ?publicationId)

    ?contributor :name ?contributorName ;
                 :affiliation ?affiliation .
    BIND(REPLACE(STR(?contributor), "^.*/([^/]*)$", "$1") AS ?contributorId)

    ?affiliation :label ?label ;
                 :institution ?institutionUri ;
                 :faculty ?facultyUri ;
                 :department ?departmentUri ;
                 :group ?groupUri .

    BIND(STR(?affiliation) as ?affiliationId)
    BIND(STR(?institutionUri) as ?institutionId)
    BIND(STR(?facultyUri) as ?facultyId)
    BIND(STR(?departmentUri) as ?departmentId)
    BIND(STR(?groupUri) as ?groupId)

    FILTER(LANG(?label) = "en")
    BIND(STR(?label) AS ?affiliationName)

  }
} LIMIT __PAGE_SIZE__ OFFSET __OFFSET__
