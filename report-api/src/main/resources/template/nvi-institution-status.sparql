PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT
  ?ARSTALL
  ?VARBEIDLOPENR
  ?ARSTALL_REG
  ?STATUS_KONTROLLERT
  ?PUBLIKASJONSFORM
  ?PUBLISERINGSKANALTYPE
  ?ISSN
  ?KVALITETSNIVAKODE
  ?PERSONLOPENR
  ?INSTITUSJONSNR
  ?AVDNR
  ?UNDAVDNR
  ?GRUPPENR
  ?ETTERNAVN
  ?FORNAVN
  ?PUBLISERINGSKANALNAVN
  ?VA_TITTEL
  ?RAPPORTSTATUS
  ?VEKTINGSTALL
  ?FAKTORTALL_SAMARBEID
  ?FORFATTERVEKT
  WHERE {
   GRAPH ?nvi {
       ?candidate a :NviCandidate ;
               :publicationId ?publicationUri ;
               :isApplicable true ;
               :internationalCollaborationFactor ?internationalCollaborationFactor ;
               :globalApprovalStatus ?RAPPORTSTATUS ;
               :publicationTypeChannelLevelPoints ?publicationTypeChannelLevelPoints ;
               :reportingYear ?reportingYear ;
               :nviContributor ?nviContributor ;
               :approval ?approval .

       FILTER (STR(?reportingYear) = "__REPLACE_WITH_REPORTING_YEAR__")

       BIND(STR(?internationalCollaborationFactor) AS ?FAKTORTALL_SAMARBEID) .
       BIND(STR(?publicationTypeChannelLevelPoints) AS ?VEKTINGSTALL) .
       BIND(STR(?reportingYear) AS ?ARSTALL)
       BIND(STR(?publicationUri) AS ?VARBEIDLOPENR)
       BIND(STR(?nviContributor) AS ?PERSONLOPENR)

       ?nviContributor :nviAffiliation ?affiliationUri .

      BIND(URI("__REPLACE_WITH_TOP_LEVEL_ORGANIZATION__") AS ?organizationUri)
      BIND(STR(?affiliationUri) AS ?affiliationUriString)

      ?approval :approvalStatus ?STATUS_KONTROLLERT ;
               :institutionId ?organizationUri ;
               :institutionPoints ?institutionPoints ;
               :creatorAffiliationPoints ?creatorAffiliationPoints ;
               :involvedOrganization ?affiliationUriString .

      ?creatorAffiliationPoints :nviCreator ?contributorIdString ;
                                :affiliation ?affiliationId ;
                                :points ?pointsForAffiliation .

       BIND(STR(?pointsForAffiliation) AS ?FORFATTERVEKT)

       BIND(STR(?affiliationUri) AS ?INSTITUSJONSNR)
       BIND(STR(?affiliationUri) AS ?AVDNR)
       BIND(STR(?affiliationUri) AS ?UNDAVDNR)
       BIND(STR(?affiliationUri) AS ?GRUPPENR)

     }
     GRAPH ?nva {
        ?publicationUri a ?instanceType ;
             :identifier ?publicationIdentifier ;
             :contributor ?contributor ;
             :year ?publicationYear ;
             :mainTitle ?VA_TITTEL ;
             :publicationContext ?publicationContext .

        BIND(STR(?publicationYear) AS ?ARSTALL_REG)
        BIND(STR(?instanceType) AS ?PUBLIKASJONSFORM)

        ?publicationContext a ?channelTypeId ;
                            :name ?PUBLISERINGSKANALNAVN ;
                            :scientificValue ?KVALITETSNIVAKODE .


        BIND(STR(?channelTypeId) AS ?PUBLISERINGSKANALTYPE)

        OPTIONAL {
            ?publicationContext :printIssn ?ISSN
        }

        ?contributor :name ?contributorName .
        BIND(?contributorName AS ?ETTERNAVN)
        BIND(?contributorName AS ?FORNAVN)
     }
    } LIMIT __PAGE_SIZE__ OFFSET __OFFSET__