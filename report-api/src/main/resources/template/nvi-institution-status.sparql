PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT
  ?ARSTALL
  ?VARBEIDLOPENR
  ?ARSTALL_REG
  ?STATUS_KONTROLLERT
  ?PUBLIKASJONSFORM
  ?PUBLISERINGSKANALTYPE
  ?ISSN
  ?KVALITETSNIVAKODE
  ?PERSONLOPENR
  ?INSTITUSJONSNR
  ?AVDNR
  ?UNDAVDNR
  ?GRUPPENR
  ?ETTERNAVN
  ?FORNAVN
  ?PUBLISERINGSKANALNAVN
  ?VA_TITTEL
  ?RAPPORTSTATUS
  ?VEKTINGSTALL
  ?FAKTORTALL_SAMARBEID
  ?FORFATTERVEKT
  WHERE {
   GRAPH ?nvi {
       ?candidate a :NviCandidate ;
               :publicationDetails ?publicationUri ;
               :isApplicable true ;
               :internationalCollaborationFactor ?FAKTORTALL_SAMARBEID ;
               :globalApprovalStatus ?RAPPORTSTATUS ;
               :publicationTypeChannelLevelPoints ?VEKTINGSTALL ;
               :reportingPeriod ?reportingPeriod .

       ?reportingPeriod a :ReportingPeriod ;
                        :year ?reportingYear .
       FILTER (STR(?reportingYear) = "__REPLACE_WITH_REPORTING_YEAR__")
       BIND(STR(?reportingYear) AS ?ARSTALL)

       BIND(STR(?publicationUri) AS ?VARBEIDLOPENR)

       ?publicationUri :contributor ?contributorId .
             ?contributorId a :NviContributor .
       BIND(STR(?contributorId) AS ?PERSONLOPENR)

       ?contributorId :affiliation ?affiliationUri .
             ?affiliationUri a ?NviOrganization .

      BIND(URI("__REPLACE_WITH_TOP_LEVEL_ORGANIZATION__") AS ?organizationUri)
      BIND(STR(?affiliationUri) AS ?affiliationUriString)

       ?candidate :approval ?approval .
       ?approval a :Approval ;
         :approvalStatus ?STATUS_KONTROLLERT ;
         :points ?institutionPoints ;
         :institutionId ?organizationUri ;
         :involvedOrganization ?affiliationUriString .

       ?institutionPoints a :InstitutionPoints ;
         :creatorAffiliationPoints ?creatorAffiliationPoints .

       ?creatorAffiliationPoints a :CreatorAffiliationPoints ;
         :nviCreator ?contributorIdString ;
         :affiliationId ?affiliationId ;
         :points ?FORFATTERVEKT .

       BIND(REPLACE(STR(?affiliationUri), "^.*/([^/]*)$", "$1") AS ?identifier)
       BIND(STRAFTER(str(?identifier), ".") AS ?temp1)
       BIND(STRAFTER(str(?temp1), ".") AS ?temp2)
       BIND(STRAFTER(str(?temp2), ".") AS ?GRUPPENR)
       BIND(STRBEFORE(str(?identifier), ".") AS ?INSTITUSJONSNR)
       BIND(STRBEFORE(str(?temp1), ".") AS ?AVDNR)
       BIND(STRBEFORE(str(?temp2), ".") AS ?UNDAVDNR)

     }
     GRAPH ?nva {
        ?publicationUri a :Publication ;
             :identifier ?publicationIdentifier ;
             :entityDescription ?entityDescription ;
             :entityDescription/:contributor ?contributor .

        ?entityDescription :publicationDate ?publicationDate .
        ?entityDescription :mainTitle ?VA_TITTEL .
        ?entityDescription :reference ?reference .
        ?reference :publicationContext ?publicationContext .
        ?publicationContext a ?channelTypeId ;
                            :name ?PUBLISERINGSKANALNAVN ;
                            :scientificValue ?scientificValue .

        ?publicationDate :year ?publicationYear .
        BIND(STR(?publicationYear) AS ?ARSTALL_REG)

        BIND(
          IF(?scientificValue = "LevelOne", "1",
          IF(?scientificValue = "LevelTwo", "2", xsd:string(?scientificValue))) AS ?KVALITETSNIVAKODE
        )

        BIND(REPLACE(STR(?publicationContext), "^.*/([^/]+)/[^/]+$", "$1") AS ?channelIdentifier)
        BIND(STR(?channelTypeId) AS ?channelType)
        BIND(REPLACE(STR(?channelType), "https://nva.sikt.no/ontology/publication#", "", "i") AS ?PUBLISERINGSKANALTYPE)
        ?reference :publicationInstance ?instance .
        ?instance a ?instanceType .
        BIND(REPLACE(STR(?instanceType), "https://nva.sikt.no/ontology/publication#", "", "i") AS ?PUBLIKASJONSFORM)

        OPTIONAL {
            ?publicationContext :printIssn ?ISSN
        }

        ?contributor :identity ?contributorId .
        ?contributorId :name ?contributorName .
        BIND(?contributorName AS ?ETTERNAVN)
        BIND(?contributorName AS ?FORNAVN)
     }
    } LIMIT __PAGE_SIZE__ OFFSET __OFFSET__