PREFIX : <https://nva.sikt.no/ontology/publication#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT
    ?publicationId
    ?status
    ?publicationTitle
    ?publicationCategory
    ?publicationDate
    ?channelType
    ?channelIdentifier
    ?channelName
    ?channelOnlineIssn
    ?channelPrintIssn
    ?channelLevel
    ?publicationIdentifier
WHERE {
  GRAPH ?g {
    ?uri a ?instanceType ;
         :status ?publicationStatus ;
         :modifiedDate ?modifiedDate ;
         :identifier ?publicationIdentifier ;
         :year ?year ;
         :mainTitle ?publicationTitle ;
         :publicationContext ?publicationContext .

    BIND(STR(?uri) AS ?publicationId)
    BIND(REPLACE(STR(?instanceType), "https://nva.sikt.no/ontology/publication#", "", "i") AS ?publicationCategory)
    BIND(REPLACE(STR(?publicationStatus), "^.*/([^/]*)$", "$1") AS ?status)

    OPTIONAL {
        ?uri :month ?month .
    }
    OPTIONAL {
        ?uri :day ?day .
    }

    BIND(IF(BOUND(?month), CONCAT("-", ?month), "") AS ?monthPart)
    BIND(IF(BOUND(?day), CONCAT("-", ?day), "") AS ?dayPart)
    BIND(CONCAT(?year, CONCAT(?monthPart, ?dayPart)) AS ?publicationDate)

    ?publicationContext a ?channelTypeId ;
                        :name ?channelName ;
                        :identifier ?channelIdentifier ;
                        :scientificValue ?channelLevel .

    BIND(STR(?channelTypeId) AS ?channelType)

    OPTIONAL {
        ?publicationContext :onlineIssn ?channelOnlineIssn
    }
    OPTIONAL {
        ?publicationContext :printIssn ?channelPrintIssn
    }
  }
}